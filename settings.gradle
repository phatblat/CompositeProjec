/*
 * settings.gradle
 */

// Configuration for the "plugins" block. Must be first in settings.
// https://docs.gradle.org/current/userguide/plugins.html#sec:plugin_management
pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
    }
    resolutionStrategy {
        eachPlugin {
            switch(requested.id.id) {
                // Workaround to make the JUnit Platform Gradle Plugin available using the `plugins` DSL
                // See: https://github.com/junit-team/junit5/issues/768#issuecomment-330078905
                case "org.junit.platform.gradle.plugin":
                    useModule("org.junit.platform:junit-platform-gradle-plugin:${requested.version}")
                    break
            }
        }
    }
}

rootProject.name = "CompositeProject"
include ':app'
include ':module'

/*
 * Composite Project configurations.
 */

// Run the 'clonePlugin' gradle task to install ShellExec in a sibling directory to build from source.
File pluginProject = file("../ShellExec")
if (pluginProject.exists()) {
    logger.lifecycle("Including $pluginProject project in composite build.")
    includeBuild(pluginProject) {
        dependencySubstitution {
            substitute module('at.phatbl:shellexec') with project(':')
        }
    }
}

// Run the 'cloneRetrofit' gradle task to install ShellExec in a sibling directory to build from source.
File retrofitProject = file("../retrofit")
if (retrofitProject.exists()) {
    logger.lifecycle("Including $retrofitProject project in composite build.")
    includeBuild(pluginProject) {
        dependencySubstitution {
            substitute module('com.squareup.retrofit2:retrofit') with project(':retrofit')
        }
    }
}
